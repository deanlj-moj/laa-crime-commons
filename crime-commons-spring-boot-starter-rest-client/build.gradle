plugins {
    id "com.javiersc.semver.project" version "0.5.0"
    id "jacoco"
    id "signing"
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "org.springframework.boot" version "3.1.5"
    id 'io.spring.dependency-management' version '1.1.4'
}

semver {
    tagPrefix.set("rc")
}

def versions = [
        commonsLang3Version    : "3.14.0"
]

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    implementation platform("io.sentry:sentry-bom:7.5.0")
    implementation("io.sentry:sentry-spring-boot-starter-jakarta")
    implementation("io.sentry:sentry-logback")

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    //spring-boot-starter-actuator library contains the Auto-configuration for the Micrometer Tracing API.
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.apache.commons:commons-lang3:$versions.commonsLang3Version"

    compileOnly "org.projectlombok:lombok"

    implementation "org.jetbrains:annotations:24.1.0"

    implementation 'io.micrometer:micrometer-tracing'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}
publishing {
    publications {
        maven(MavenPublication) {

            from components.java

            pom {
                artifactId = project.name
                name = "Sprint Boot Starter REST Client"
                url = "https://github.com/ministryofjustice/laa-crime-commons"
                description = "A Spring Boot auto-configuration library that facilitates communication with LAA Crime APIs"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                organization {
                    name = "Justice Digital"
                }
                developers {
                    developer {
                        id = "mhowell494"
                        name = "Matthew Howell"
                        email = "matthew.howell@digital.justice.gov.uk"
                    }
                }
                scm {
                    url = "https://github.com/ministryofjustice/laa-crime-commons.git"
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty("semver.stage")) {
                if (project.property("semver.stage") == "snapshot") {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/"
                }
            }
            credentials {
                username = project.findProperty("sonatypeUsername") ?: System.getenv("OSSRH_USERNAME")
                password = project.findProperty("sonatypePassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}



signing {
    sign publishing.publications.maven
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

tasks.named("test") {
    useJUnitPlatform()
}
