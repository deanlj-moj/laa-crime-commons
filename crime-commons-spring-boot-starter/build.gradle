import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id "nebula.contacts" version "6.0.0"
    id "nebula.maven-publish" version "18.4.0"
    id 'org.springframework.boot' version '3.0.4' apply false
}

version = '0.0.2'
sourceCompatibility = '17'
group = 'uk.gov.justice.laa.crime'
description = 'Crime Applications Shared Libraries'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

contacts {
    'laa-crime-apps@digital.justice.gov.uk' {
        moniker 'Crime Applications Team'
        role 'owner'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

    compileOnly 'org.projectlombok:lombok'

    implementation 'org.jetbrains:annotations:23.0.0'

    implementation platform('io.sentry:sentry-bom:6.15.0')
    implementation('io.sentry:sentry-spring-boot-starter')
    implementation('io.sentry:sentry-logback')


    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ministryofjustice/laa-crime-commons")
            credentials {
                password = project.findProperty("github-token") ?: System.getenv("GITHUB_TOKEN")
                username = project.findProperty("github-username") ?: System.getenv("GITHUB_USERNAME")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
