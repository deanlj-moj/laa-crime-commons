import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.0.4' apply false
}

version = '0.0.2'
sourceCompatibility = '17'
group = 'uk.gov.justice.laa.crime'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

    compileOnly 'org.projectlombok:lombok'

    implementation 'org.jetbrains:annotations:23.0.0'

    implementation platform('io.sentry:sentry-bom:6.15.0')
    implementation('io.sentry:sentry-spring-boot-starter')
    implementation('io.sentry:sentry-logback')


    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ministryofjustice/laa-crime-commons")
            credentials {
                password = project.findProperty("github-token") ?: System.getenv("GITHUB_TOKEN")
                username = project.findProperty("github-username") ?: System.getenv("GITHUB_USERNAME")
            }
        }
    }
    publications {
        CrimeCommons(MavenPublication) {
            from components.java

            pom {
                name = 'Crime Commons Spring Boot Starter'
                description = 'Crime Applications Shared Libraries'
                url = 'http://www.example.com/library'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        name = 'Matthew Howell'
                        email = 'matthew.howell@digital.justice.gov.uk'
                    }
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
