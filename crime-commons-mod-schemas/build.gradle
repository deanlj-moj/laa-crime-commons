plugins {
    id "com.javiersc.semver.project" version "0.5.0"
    id "jacoco"
    id "signing"
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "org.springframework.boot" version "3.1.5" apply false
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.jsonschema2dataclass" version "6.0.0"
}

semver {
    tagPrefix.set("crime-commons-mod-schemas-")
}

def versions = [
        jacksonVersion      : "2.17.0",
        validationapiVersion: "3.1.0-M2",
        crimeCommonsClasses : "3.18.0"
]

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

shadowJar {
    dependencies {
        exclude(dependency("com.fasterxml.jackson.core:jackson-databind:$versions.jacksonVersion"))
        exclude(dependency("jakarta.validation:jakarta.validation-api:$versions.validationapiVersion"))
    }
}

dependencies {
    implementation project(":crime-commons-classes")
    implementation "com.fasterxml.jackson.core:jackson-databind:$versions.jacksonVersion"
    compileOnly "jakarta.validation:jakarta.validation-api:$versions.validationapiVersion"
}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java

            pom {
                artifactId = project.name
                name = "Crime Apps Common modernised services Schemas"
                url = "https://github.com/ministryofjustice/laa-crime-commons"
                description = "A library of shared modernised services schemas"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                organization {
                    name = "Justice Digital"
                }
                developers {
                    developer {
                        id = "Sriram"
                        name = "Sriram Kasthuri"
                        email = "sriram.kasthuri@digital.justice.gov.uk"
                    }
                }
                scm {
                    url = "https://github.com/ministryofjustice/laa-crime-commons.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
}

jsonSchema2Pojo {
    executions {
        main {
            io.source.setFrom files("${project.rootDir}/crime-commons-mod-schemas/src/main/resources/schemas")
            klass.targetPackage = 'uk.gov.justice.laa.crime.common.model'
            methods.builders = true
            methods.annotateJsr303Jakarta = true
            methods.buildersDynamic = true
            fields.floatUseBigDecimal = true
            dateTime.dateType = "java.time.LocalDate"
            dateTime.dateTimeType = "java.time.LocalDateTime"
        }
    }
}

tasks.sourcesJar.dependsOn("generateJsonSchema2DataClassConfigMain")


